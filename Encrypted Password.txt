OBJECT Table 50000 Password Encryption Sample
{
  OBJECT-PROPERTIES
  {
    Date=10/02/18;
    Time=10:49:42;
    Modified=Yes;
    Version List=PasswordEncryption;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10         }
    { 2   ;   ;Username            ;Text30         }
    { 3   ;   ;Password Key        ;GUID           }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServicePassword@1000000000 : Record 1261;

    PROCEDURE SetPassword@1000000000(PasswordText@1000000000 : Text[215]);
    BEGIN
      IF ISNULLGUID("Password Key") OR NOT ServicePassword.GET("Password Key") THEN BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.INSERT(TRUE);
        "Password Key" := ServicePassword.Key;
      END ELSE BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.MODIFY;
      END;
    END;

    PROCEDURE GetPassword@1000000001() : Text;
    BEGIN
      IF NOT ISNULLGUID("Password Key") THEN
        IF ServicePassword.GET("Password Key") THEN
          EXIT(ServicePassword.GetPassword());
    END;

    BEGIN
    {
      Build: 8.00.47871

      It uses ENCRYPT statement which exist from 2015 onwards.

      Patterns: 1.Sensitive Data Encapsulation.
                2.Masked Text
    }
    END.
  }
}

OBJECT Page 50000 Password Encryption Sample
{
  OBJECT-PROPERTIES
  {
    Date=10/02/18;
    Time=10:49:28;
    Modified=Yes;
    Version List=PasswordEncryption;
  }
  PROPERTIES
  {
    SourceTable=Table50000;
    PageType=Card;
    OnOpenPage=BEGIN
                 RESET;
                 IF NOT GET THEN BEGIN
                   INIT;
                   INSERT;
                 END;

                 Password := GetPassword;
               END;

  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=General;
                GroupType=Group }

    { 1000000002;2;Field  ;
                SourceExpr=Username }

    { 1000000003;2;Field  ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Password;
                SourceExpr=Password;
                OnValidate=BEGIN
                             SetPassword(Password);
                           END;
                            }

  }
  CODE
  {
    VAR
      Password@1000000000 : Text[215];
      PswEncryptionSample@1000000001 : Record 50000;

    BEGIN
    {
      Build: 8.00.47871

      It uses ENCRYPT statement which exist from 2015 onwards.

      Patterns: 1.Sensitive Data Encapsulation.
                2.Masked Text
    }
    END.
  }
}

